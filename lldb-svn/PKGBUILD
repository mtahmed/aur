# Maintainer: Muhammad Tauqir Ahmad <mtahmed@uwaterloo.ca>
# Contributor: Nicolas Hureau <aur@kalenz.fr>
# Contributor: Philipp Sieweck <psi@informatik.uni-kiel.de>
# Contributor: Xavier de Gaye <xdegaye@gmail.com>

pkgname=lldb-svn
_gcc_ver=4.6.2
pkgver=196543
pkgrel=1
pkgdesc="The LLDB Debugger"
arch=('i686' 'x86_64')
url="http://llvm.org/"
license=('custom:University of Illinois/NCSA')
depends=('gcc-libs' 'libffi' 'python2' "gcc>=$_gcc_ver" 'libedit' 'llvm' 'clang')
makedepends=('svn' 'cmake' 'swig')
provides=('lldb')
conflicts=('lldb')
source=(
  'llvm::svn+http://llvm.org/svn/llvm-project/llvm/trunk#revision=196543'
  'cfe::svn+http://llvm.org/svn/llvm-project/cfe/trunk#revision=196543'
  'lldb::svn+http://llvm.org/svn/llvm-project/lldb/trunk#revision=196543'
  'usr_bin_env_python.patch'
)
sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  '139bbfb4113bf147b9cfcf5b74f0fb17239db476d0f357ccdefb85453c0023ce'
)

pkgver() {
  cd "$srcdir/lldb"
  echo $(svnversion)
}

build() {
  cd "$srcdir"
  msg2 "Exporting Clang in the LLVM tree ..."
  svn export cfe llvm/tools/clang &> /dev/null
  msg2 "Exporting LLDB in the LLVM tree ..."
  svn export lldb llvm/tools/lldb &> /dev/null

  msg2 "Applying linux-temporary patches ..."

  cd "$srcdir/llvm"

  patch -p1 < ../usr_bin_env_python.patch

  cd "$srcdir/llvm/tools/lldb"

  msg2 "Applying Archlinux-specific patch ..."

  sed -i -e "s|python-config|python2-config|" lib/Makefile
  sed -i -e "s|python-config|python2-config|" Makefile
  sed -i -e "s|/usr/bin/env python|&2|" scripts/Python/build-swig-Python.sh
  sed -i -e "s|/usr/bin/env python|&2|" scripts/Python/finish-swig-Python-LLDB.sh

  cd "$srcdir/llvm"
  msg2 "Starting build ..."

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  export CFLAGS="$CFLAGS -fno-tree-pre"
  export CXXFLAGS="$CXXFLAGS -fno-tree-pre"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLLVM_ENABLE_CXX11=ON \
    -LLVM_ENABLE_ASSERTIONS=OFF \
    -LLVM_ENABLE_FFI=ON \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    ..

  make
}

package() {
  cd "$srcdir/llvm"

  # Install the license
  install -Dm644 tools/lldb/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$srcdir/llvm/build"

  # Install the lldb binary
  install -Dm755 bin/lldb "$pkgdir/usr/bin/lldb"

  # Install the lldb library
  install -Dm755 lib/liblldb.so "$pkgdir/usr/lib/liblldb.so"

  # Install the lldb python libraries.
  python_dir="$pkgdir/usr/lib/python2.7/site-packages"
  mkdir -p "$python_dir"
  cp -a lib/python2.7/site-packages/lldb "$python_dir"

  # Relink the _lldb.so for python
  ln -sf /usr/lib/liblldb.so "$python_dir/lldb/_lldb.so"
}

# vim:set sts=2 sw=2 et:
